buildscript {
    ext.versions = [
            "kotlin" : "1.3.61",
            "jackson": "2.10.2",
    ]

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }

    dependencies {
        classpath(
                "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0",
                "org.jetbrains.kotlin:kotlin-allopen:${versions["kotlin"]}",
                "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions["kotlin"]}",
                "org.jetbrains.kotlin:kotlin-noarg:${versions["kotlin"]}",
                "org.jetbrains.kotlin:kotlin-serialization:${versions["kotlin"]}",
        )
    }
}

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

allprojects {
    group 'com.luggsoft.k4'
    version '1.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/dokka"
        subProjects = [
                "k4-cli",
                "k4-core",
                "k4-example",
        ]

        configuration {
            includes = [
                    "README.md",
            ]
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "kotlin-allopen"
    apply plugin: "kotlin-noarg"
    apply plugin: "org.jetbrains.kotlin.jvm"

    sourceCompatibility = "1.8"

    dependencies {
        api(
                "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${versions['jackson']}",
                "com.fasterxml.jackson.module:jackson-module-kotlin:${versions['jackson']}",
                "org.apache.logging.log4j:log4j-api:2.13.0",
                "org.apache.logging.log4j:log4j-core:2.13.0",
                "org.apache.logging.log4j:log4j-slf4j-impl:2.13.0",
                "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
        )

        testImplementation(
                "org.junit.jupiter:junit-jupiter:5.6.0",
        )
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    test {
        useJUnitPlatform()

        filter {
            includeTestsMatching "*Test"
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/luggsoft/k4")
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}
